import "@stdlib/deploy";
import "@stdlib/ownable";

message RegisterBot {
    manifestUrl: String;
}
message SetWalletManifest {
    // nonce: Int as uint64;
    manifestUrl: String;
}
message WalletInfo {
    manifestUrl: String;
    owner: Address;
    master: Address;
}
message WalletManifestChangedEvent {
    oldManifestUrl: String;
    newManifestUrl: String;
}
message WalletCreatedEvent {
    wallet: Address;
    owner: Address;
}
message WalletWithdraw {
}
message SubmitAttestTask {
    // task id will be generated by the master contract
    taskId: Int? as uint64;
    // status of the task, can be "pending", "verified" or "rejected"
    status: String? = "pending";
    walletAddress: Address?;
    proofUrl: String;
    // attestValue is the value that will be paid to the attester,
    // the value in the message should be greater than attestValue.
    attestValue: Int as uint32;
}

// this is the master contract of TON.
contract Attest with Deployable, Ownable {
    owner: Address;
    // setManifestNounce: map<Address, Int as uint32> = emptyMap();
    attestTaskId: Int as uint64 = 0;

    init(){
        self.owner = sender();
    }

    // create AttestWallet for the sender
    receive(msg: RegisterBot){
        let walletInit: StateInit = self.getWalletInit(sender());
        let walletAddr: Address = contractAddress(walletInit);
        // let nonce = self.getManifestNonce(walletAddr);

        send(SendParameters{
                to: walletAddr,
                value: 0,
                bounce: true,
                mode: SendRemainingValue,
                body: SetWalletManifest{
                    // nonce: nonce,
                    manifestUrl: msg.manifestUrl
                }.toCell(),
                code: walletInit.code,
                data: walletInit.data
            }
        );
        // self.setManifestNonce.set(walletAddr, nonce + 1);
    }

    fun getWalletInit(walletOwner: Address): StateInit {
        return initOf AttestWallet(myAddress(), walletOwner);
    }

    // fun getManifestNonce(walletOwner: Address): Int {
    //     let nonce: Int = 0;
    //     if (self.setManifestNonce.get(sender()) != null) {
    //         nonce = self.setManifestNonce.get(sender())!!;
    //     }
    //     return nonce;
    // }

    // update manifest of AttestWallet
    receive(msg: SetWalletManifest){
        let walletInit: StateInit = self.getWalletInit(sender());
        let walletAddr: Address = contractAddress(walletInit);

        send(SendParameters{
                to: walletAddr,
                value: 0,
                bounce: true,
                mode: SendRemainingValue,
                body: SetWalletManifest{
                    // nonce: nonce,
                    manifestUrl: msg.manifestUrl
                }.toCell()
            }
        );
        // self.setManifestNonce.set(walletAddr, nonce + 1);
    }

    // submit attest task
    receive(msg: SubmitAttestTask){
        let ctx = context();
        require(ctx.value >= msg.attestValue, "Attest value not enough");
        require(msg.attestValue > ton("0.05"), "Attest value should be greater than 0.05");

        let walletInit: StateInit = self.getWalletInit(sender());
        let walletAddr: Address = contractAddress(walletInit);

        msg.taskId = self.attestTaskId;
        msg.status = "pending";
        msg.walletAddress = walletAddr;

        // notify to the submiter
        self.notify(msg.toCell());

        // send the task to AttestWallet
        send(SendParameters{
                to: walletAddr,
                value: 0,
                bounce: true,
                mode: SendRemainingValue,
                body: msg.toCell()
            }
        );

        self.attestTaskId = self.attestTaskId + 1;
    }
}

contract AttestWallet with Ownable {
    owner: Address;
    master: Address;
    manifestUrl: String = "";
    // manifestNonce: Int as uint64 = 0;
    // attestTaskIncentives is the balance of the attest task,
    // the key is the task id, the value is the incentive of the task.
    attestTaskIncentives: map<Int as uint64, Int> = emptyMap();
    // lockedValue is the value that is locked in the contract, cannot be withdrawed.
    lockedValue: Int = ton("0.1");

    init(master: Address, owner: Address){
        self.master = master;
        self.owner = owner;
    }

    // master can set manifest of AttestWallet
    receive(msg: SetWalletManifest){
        require(self.master == sender(), "Only master can set manifest");
        // require(msg.nonce == self.manifestNonce, "Nonce not match");

        emit(WalletManifestChangedEvent{
            oldManifestUrl: self.manifestUrl,
            newManifestUrl: msg.manifestUrl
        }.toCell());

        self.manifestUrl = msg.manifestUrl;
        // self.manifestNonce = self.manifestNonce + 1;
    }

    // owner can withdraw all balance
    receive(msg: WalletWithdraw){
        require(self.owner == sender(), "Only master can withdraw");
        require(myBalance() > self.lockedValue, "Balance not enough");
        send(SendParameters{
                to: sender(),
                value: (myBalance() - context().value) - self.lockedValue,
                bounce: true,
                mode: SendRemainingValue
            }
        );
    }

    // submit attest task
    receive(msg: SubmitAttestTask){
        let ctx = context();
        require(ctx.value >= msg.attestValue, "Attest value not enough");
        require(msg.attestValue > ton("0.05"), "Attest value should be greater than 0.05");

        emit(msg.toCell());
        self.lockedValue = self.lockedValue + msg.attestValue;
    }

    get fun master(): String {
        return self.master.toString();
    }

    get fun manifestUrl(): String {
        return self.manifestUrl;
    }

    get fun taskIncentives(taskId: Int): Int? {
        return self.attestTaskIncentives.get(taskId);
    }
}
